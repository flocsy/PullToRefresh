buildscript {
    apply from: "${rootProject.projectDir}/dependencies.gradle"
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlin_version}"
    }
}

apply from: "${rootProject.projectDir}/dependencies.gradle"
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    buildToolsVersion project.buildToolsVersion
    compileSdkVersion project.compileSdkVersion
    defaultConfig {
        applicationId "com.fletech.android.pulltorefresh.demo"
        minSdkVersion project.minSdkVersion
        targetSdkVersion project.targetSdkVersion
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    def isRelease = file("release.keystore").exists()
    if (isRelease) {
        signingConfigs {
            release {
                storeFile file("release.keystore")
                storePassword releaseKeystorePassword
                keyAlias releaseKeystoreAlias
                keyPassword releaseKeystorePassword
            }
        }
    }
    buildTypes {
        release {
            signingConfig isRelease ? signingConfigs.release : signingConfigs.debug
            useProguard true
            minifyEnabled true
            shrinkResources true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation project(':pulltorefresh')

    testImplementation "junit:junit:${junit_version}"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:${espresso_version}"

    implementation(
            "com.android.support.constraint:constraint-layout:${constraint_layout_version}",
            "com.android.support:appcompat-v7:${android_support_version}",
            "com.android.support:design:${android_support_version}",
    )
    debugImplementation "com.squareup.leakcanary:leakcanary-android:${leakcanary_version}"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:${leakcanary_version}"
}
